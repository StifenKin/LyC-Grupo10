package lyc.compiler;

import java_cup.runtime.*;
import java.util.Set;
import java.util.HashSet;
import java.util.HashMap;
import java.util.Map;
import lyc.compiler.table.*;
import lyc.compiler.files.IntermediateCodeGenerator;
import lyc.compiler.files.Operator;
import lyc.compiler.files.Triplet;
import lyc.compiler.files.TypeTable;
import java.util.HashMap;
import lyc.compiler.model.*;


class Parser;

action code {:
    Set<String> idList = new HashSet<>();
    String whileStartLabel = null;
    String ifEndLabel = null;
:}


// Terminals
terminal ASSIG;
terminal PLUS;
terminal SUB;
terminal MULT;
terminal DIV;
terminal MOD;
terminal REST;
terminal MAYOR;
terminal LOWER;
terminal MAYOR_I;
terminal LOWER_I;
terminal EQUAL;
terminal NOT_EQUAL;
terminal AND_COND;
terminal OR_COND;
terminal NOT_COND;
terminal OPEN_BRACKET;
terminal CLOSE_BRACKET;
terminal OPEN_CURLY_BRACKET;
terminal CLOSE_CURLY_BRACKET;
terminal OPEN_SQUARE_BRACKET;
terminal CLOSE_SQUARE_BRACKET;
terminal COMMA;
terminal SEMI_COLON;
terminal DOT;
terminal DOUBLE_DOT;
terminal String INTEGER_CONSTANT;
terminal String FLOAT_CONSTANT;
terminal String STRING_CONSTANT;
terminal BOOLEAN_CONSTANT;
terminal INT;
terminal FLOAT;
terminal STRING;
terminal IF;
terminal ELSE;
terminal WHILE;
terminal WRITE;
terminal READ;
terminal String IDENTIFIER;
terminal INIT;
terminal ISZERO;
terminal CONVDATE;
terminal DATECONVERTED;
terminal DATE_LITERAL;

// Non Terminals
non terminal program;
non terminal block;
non terminal sentence;
non terminal var_declaration;
non terminal var_sencente_dec;
non terminal DataType data_type;
non terminal id_list;
non terminal id_item;
non terminal decision;
non terminal String condition;
non terminal Integer comparison;
non terminal String comparator;
non terminal iterator;
non terminal assignment;
non terminal s_write;
non terminal s_read;
non terminal String write_param;
non terminal String read_param;
non terminal Integer expression;
non terminal Integer term;
non terminal Integer factor;
non terminal Integer is_zero;
non terminal conv_date;
non terminal c_list;
non terminal list;
non terminal sep;
non terminal if_head;
non terminal else_head;
non terminal opt_block;
non terminal it_head;
non terminal String it_start;
non terminal var_body;


// Start Symbol Definition
start with program;

program   ::= opt_block {: System.out.println("Compilation Ok"); :};


opt_block ::= block
            | /* empty */; //Uso de LAMBDA.

sep ::= {: System.out.println("----------------Nueva Sentencia---------------"); :};

block ::= sentence
        | block sep sentence;


sentence ::=
        var_declaration
       |  decision
       |  iterator
       |  assignment
       |  s_write
       |  s_read
       |  is_zero
       |  conv_date;



var_declaration ::= INIT {: System.out.println("Variable declaration starts"); :} OPEN_CURLY_BRACKET var_body CLOSE_CURLY_BRACKET {: System.out.println("Variable declaration ends"); :};

var_body ::= var_sencente_dec var_body;
var_body ::= var_sencente_dec;
var_sencente_dec ::= id_list DOUBLE_DOT data_type:dt {:
    for (String id : idList) {
        SymbolTableManager.setDataTypeInTable(id, dt);  
        IntermediateCodeGenerator.createTriplet(Operator.DECLARE, id, dt);
    }
    idList.clear();
:};

data_type ::=
              INT {:
                System.out.println("Int data type");
                RESULT = DataType.INTEGER_TYPE;
            :}
            | FLOAT {:
                System.out.println("Float data type");
                RESULT = DataType.FLOAT_TYPE;
            :}
            | STRING {:
                System.out.println("String data type");
                RESULT = DataType.STRING_TYPE;
            :}
            | DATECONVERTED {:
                System.out.println("DateConverted data type");
                RESULT = DataType.STRING_TYPE;
            :};

id_list ::=
      id_item
    | id_list COMMA id_item
    ;



id_item ::= IDENTIFIER:id {:
    SymbolEntry entry = SymbolTableManager.get(id);

    if (entry.getDataType() != DataType.ID) {
        // Ya tenía tipo asignado → redeclaración
        throw new DuplicatedIdentifier("Identificador '" + id + "' ya declarado antes");
    }

    if (!idList.add(id)) {
        throw new DuplicatedIdentifier("Identificador repetido en la misma declaración: " + id);
    }
:};


if_head ::= IF {: System.out.println("If statement"); :};

else_head ::= ELSE {: System.out.println("Else branch"); :};

decision ::= if_head OPEN_BRACKET condition:endLabel CLOSE_BRACKET OPEN_CURLY_BRACKET opt_block CLOSE_CURLY_BRACKET {:
    // Crear label de fin del if
    IntermediateCodeGenerator.createLabel(endLabel);
:};

decision ::= if_head OPEN_BRACKET condition:elseLabel CLOSE_BRACKET OPEN_CURLY_BRACKET opt_block CLOSE_CURLY_BRACKET
                else_head {:
    // Salto incondicional al final después del then
    ifEndLabel = IntermediateCodeGenerator.generateLabel();
    IntermediateCodeGenerator.createTriplet(Operator.BI, ifEndLabel, (String)null);
    // Label del else
    IntermediateCodeGenerator.createLabel(elseLabel);
:} OPEN_CURLY_BRACKET opt_block CLOSE_CURLY_BRACKET {:
    // Label de fin
    IntermediateCodeGenerator.createLabel(ifEndLabel);
:};


condition ::= is_zero:jumpIdx {:
    // isZero retorna el índice del terceto de salto, igual que comparison
    String targetLabel = IntermediateCodeGenerator.generateLabel();
    IntermediateCodeGenerator.updateJumpTarget(jumpIdx, targetLabel);
    RESULT = targetLabel;
:};

condition ::= comparison:cmp1 AND_COND comparison:cmp2 {:
    System.out.println("And condition");
    // Para AND: ambas comparaciones deben saltar al mismo label de salida
    String targetLabel = IntermediateCodeGenerator.generateLabel();
    // Actualizar ambos saltos para que apunten al mismo label
    IntermediateCodeGenerator.updateJumpTarget(cmp1, targetLabel);
    IntermediateCodeGenerator.updateJumpTarget(cmp2, targetLabel);
    RESULT = targetLabel;
:};

condition ::= comparison:cmp1 OR_COND comparison:cmp2 {:
    System.out.println("Or condition");
    // Para OR: la primera salta a evaluar la segunda si es falsa
    // La segunda salta al label de salida si es falsa
    String targetLabel = IntermediateCodeGenerator.generateLabel();
    String nextCheckLabel = IntermediateCodeGenerator.generateLabel();

    // La primera comparación salta a la segunda comparación si es falsa
    IntermediateCodeGenerator.updateJumpTarget(cmp1, nextCheckLabel);
    // Crear el label para la segunda comparación
    IntermediateCodeGenerator.createLabel(nextCheckLabel);
    // La segunda comparación salta al label de salida si es falsa
    IntermediateCodeGenerator.updateJumpTarget(cmp2, targetLabel);

    RESULT = targetLabel;
:};

condition ::= comparison:jumpIdx {:
    System.out.println("Condition = Comparison");
    // Generar un label de salida y actualizar el salto
    String targetLabel = IntermediateCodeGenerator.generateLabel();
    IntermediateCodeGenerator.updateJumpTarget(jumpIdx, targetLabel);
    RESULT = targetLabel;
:};

condition ::= NOT_COND comparison:cmp {:
    System.out.println("Not condition");
    // Para NOT: necesitamos invertir la lógica
    // Ya usamos saltos negados, así que NOT invierte nuevamente
    String targetLabel = IntermediateCodeGenerator.generateLabel();
    IntermediateCodeGenerator.updateJumpTarget(cmp, targetLabel);
    RESULT = targetLabel;
:};

condition ::= NOT_COND OPEN_BRACKET condition:cond CLOSE_BRACKET {:
    System.out.println("Not condition with parentheses");
    RESULT = cond;
:};

condition ::= OPEN_BRACKET condition:cond CLOSE_BRACKET {:
    System.out.println("Parenthesized condition");
    RESULT = cond;
:};

comparison ::= expression:e1 comparator:c expression:e2 {:
    System.out.println("Comparison detected: " + e1 + " " + c + " " + e2);

    // Generar los tercetos: CMP y salto negado con un placeholder
    // Retornar el índice del terceto de salto para que condition lo actualice
    int jumpIdx = IntermediateCodeGenerator.createComparison(e1, e2, c, "PLACEHOLDER");

    RESULT = jumpIdx;
:};



comparator ::= MAYOR {: RESULT = ">"; :}
             | LOWER {: RESULT = "<"; :}
             | MAYOR_I {: RESULT = ">="; :}
             | LOWER_I {: RESULT = "<="; :}
             | EQUAL {: RESULT = "=="; :}
             | NOT_EQUAL {: RESULT = "!="; :}
             ;


it_head ::= WHILE {: System.out.println("While statement"); :};

iterator ::= it_head {:
    // Crear label de inicio del while
    whileStartLabel = IntermediateCodeGenerator.generateLabel();
    IntermediateCodeGenerator.createLabel(whileStartLabel);
:} OPEN_BRACKET condition:endLabel CLOSE_BRACKET OPEN_CURLY_BRACKET opt_block CLOSE_CURLY_BRACKET {:
    // Salto incondicional al inicio
    IntermediateCodeGenerator.createTriplet(Operator.BI, whileStartLabel, (String)null);
    // Label de salida
    IntermediateCodeGenerator.createLabel(endLabel);
:};


assignment ::=
    IDENTIFIER:id ASSIG expression:e
    {:          
    
        System.out.println("Assignment detected");
        System.out.println(id + " := " + (e != null ? e : "ERROR"));
        SymbolEntry entry = SymbolTableManager.get(id);
        if(!entry.isDeclared())
            throw new UndefinedIdentifierException("El identificador: "+id+ " no fue declarado");

        DataType dst = entry.getDataType();
        DataType src = TypeTable.getType(e);

        if (!TypeTable.areCompatible(dst, src, Operator.ASSIGN)) {
          throw new TypeMismatchException("Tipos incompatibles en asignación: " + dst + " := " + src);
        }

        IntermediateCodeGenerator.createTriplet(Operator.ASSIGN, id, e);
    :}
  | IDENTIFIER:id ASSIG STRING_CONSTANT:s
    {:  

        System.out.println("Assignment detected");
        System.out.println(id+ " = "+ s );

        SymbolEntry entry = SymbolTableManager.get(id);
        if(!entry.isDeclared())
            throw new UndefinedIdentifierException("El identificador: "+id+ " no fue declarado");

        int litIdx = IntermediateCodeGenerator.createTriplet(Operator.CTE, s).getIndex();
        TypeTable.putType(litIdx, DataType.STRING_TYPE);

        DataType dst = entry.getDataType();
        DataType src = TypeTable.getType(litIdx);

        if (!TypeTable.areCompatible(dst, src, Operator.ASSIGN)) {
            throw new TypeMismatchException("Tipos incompatibles en asignación: " + dst + " := " + src);
        }

        IntermediateCodeGenerator.createTriplet(Operator.ASSIGN, id, s);
    :}
  | IDENTIFIER:id ASSIG conv_date:cd
    {:  
        System.out.println("Assignment detected");
        System.out.println(id+ " = "+ cd );
        RESULT = cd;
    :};


expression ::= term:t {:
    RESULT = t;
:};

expression ::= expression:e PLUS term:t {:
    System.out.println("Suma detectada: " + e + " + " + t);

    DataType t1 = TypeTable.getType(e);
    DataType t2 = TypeTable.getType(t);

    if (!TypeTable.areCompatible(t1, t2, Operator.ADD)) {
        throw new TypeMismatchException("Tipos incompatibles para +: " + t1 + " y " + t2);
    }

    DataType rType = TypeTable.resultType(t1, t2, Operator.ADD);
    int idx = IntermediateCodeGenerator.createTriplet(Operator.ADD, e, t).getIndex();
    RESULT = idx;
    TypeTable.putType(idx, rType);
:};

expression ::= expression:e SUB term:t {:
    System.out.println("Resta detectada: " + e + " - " + t);

    DataType t1 = TypeTable.getType(e);
    DataType t2 = TypeTable.getType(t);

    if (!TypeTable.areCompatible(t1, t2, Operator.SUB)) {
        throw new TypeMismatchException("Tipos incompatibles para -: " + t1 + " y " + t2);
    }

    DataType rType = TypeTable.resultType(t1, t2, Operator.SUB);
    int idx = IntermediateCodeGenerator.createTriplet(Operator.SUB, e, t).getIndex();
    RESULT = idx;
    TypeTable.putType(idx, rType);
:};


term ::= factor:f {:
    RESULT = f;
:};

term ::= term:tt MULT factor:ff {:
    System.out.println("Multiplicación detectada: " + tt + " * " + ff);

    DataType t1 = TypeTable.getType(tt);
    DataType t2 = TypeTable.getType(ff);

    if (!TypeTable.areCompatible(t1, t2, Operator.MUL)) {
        throw new TypeMismatchException(
            "Tipos incompatibles para *: " + t1 + " y " + t2
        );
    }
    DataType rType = TypeTable.resultType(t1, t2, Operator.MUL);

    int idx = IntermediateCodeGenerator.createTriplet(Operator.MUL, tt, ff).getIndex();
    RESULT = idx;
    TypeTable.putType(idx, rType);
:};

term ::= term:tt DIV factor:ff {:
    System.out.println("División detectada: " + tt + " / " + ff);

    DataType t1 = TypeTable.getType(tt);
    DataType t2 = TypeTable.getType(ff);

    if (!TypeTable.areCompatible(t1, t2, Operator.DIV)) {
        throw new TypeMismatchException(
            "Tipos incompatibles para /: " + t1 + " y " + t2
        );
    }
    DataType rType = TypeTable.resultType(t1, t2, Operator.DIV);

    int idx = IntermediateCodeGenerator.createTriplet(Operator.DIV, tt, ff).getIndex();
    RESULT = idx;
    TypeTable.putType(idx, rType);
:};

term ::= term:tt MOD factor:ff {:
    System.out.println("Módulo detectada: " + tt + " % " + ff);

    DataType t1 = TypeTable.getType(tt);
    DataType t2 = TypeTable.getType(ff);

    if (!TypeTable.areCompatible(t1, t2, Operator.MOD)) {
        throw new TypeMismatchException(
            "Tipos incompatibles para %: " + t1 + " y " + t2
        );
    }
    DataType rType = TypeTable.resultType(t1, t2, Operator.MOD);

    int idx = IntermediateCodeGenerator.createTriplet(Operator.MOD, tt, ff).getIndex();
    RESULT = idx;
    TypeTable.putType(idx, rType);
:};

factor ::= SUB factor:f {:
    System.out.println("Unario: -" + f);
    DataType t = TypeTable.getType(f);
    
    if (!t.isNumeric()) {
        throw new TypeMismatchException("El operador '-' unario requiere tipo numérico. Encontrado: " + t);
    }
    int idx = IntermediateCodeGenerator.createTriplet(Operator.NEG, f).getIndex();
    RESULT = idx;
    TypeTable.putType(idx, t);
:};

factor ::= IDENTIFIER:id {:
    SymbolEntry entry = SymbolTableManager.get(id);
    if(!entry.isDeclared())
        throw new UndefinedIdentifierException("La variable id: "+id+" no fue declarada");

    System.out.println("Id: " + id);
    int idx = IntermediateCodeGenerator.createTriplet(Operator.ID, id).getIndex();
    RESULT = idx;

    DataType t = SymbolTableManager.get(id).getDataType();
    TypeTable.putType(idx, t);

:};

factor ::= INTEGER_CONSTANT:constant {:
    System.out.println("Integer constant: " + constant);
    int idx = IntermediateCodeGenerator.createTriplet(Operator.CTE, constant).getIndex();
    RESULT = idx;
    TypeTable.putType(idx, DataType.INTEGER_TYPE);
:};

factor ::= FLOAT_CONSTANT:constant {:
    System.out.println("Float constant: " + constant);
    int idx = IntermediateCodeGenerator.createTriplet(Operator.CTE, constant).getIndex();
    RESULT = idx;
    TypeTable.putType(idx, DataType.FLOAT_TYPE);
:};

factor ::= OPEN_BRACKET expression:e CLOSE_BRACKET {:
    RESULT = e;
:};

s_write ::=
    WRITE OPEN_BRACKET write_param:wp CLOSE_BRACKET
    {:
        System.out.println("Write statement");
        System.out.println("Write parameter: " + wp);

        IntermediateCodeGenerator.createTriplet(Operator.WRITE, wp);
    :};

write_param ::=
      STRING_CONSTANT:sc
        {: RESULT = sc; :}
    | IDENTIFIER:id
        {: RESULT = id; :};

s_read ::= 
    READ OPEN_BRACKET read_param:rp CLOSE_BRACKET 
    {: 
        System.out.println("Read statement"); 
        System.out.println("Read parameter: " + rp);

        SymbolEntry entry = SymbolTableManager.get(rp);
        if (entry == null || !entry.isDeclared()) {
            throw new UndefinedIdentifierException("La variable: " + rp + " no fue declarada");
        }

        IntermediateCodeGenerator.createTriplet(Operator.READ, entry.getName());
    :};
read_param ::= 
    IDENTIFIER:id
        {: RESULT = id; :};


//isZero

is_zero ::= ISZERO OPEN_BRACKET expression:e CLOSE_BRACKET
              {:
                System.out.println("Condition = isZero(" + e + ")");

                // isZero(expr) es equivalente a expr == 0
                // Generar terceto con la constante 0
                int zeroIdx = IntermediateCodeGenerator.createTriplet(Operator.CTE, "_0").getIndex();
                TypeTable.putType(zeroIdx, TypeTable.getType(e)); // Mismo tipo que la expresión

                // Generar CMP comparando la expresión con 0
                IntermediateCodeGenerator.createTriplet(Operator.CMP, e, zeroIdx);

                // Generar salto complementario (BNE para ==)
                // Si expr != 0 (negación de ==), salta al label
                Triplet jumpTriplet = IntermediateCodeGenerator.createTriplet(Operator.BNE, "PLACEHOLDER", (String)null);

                RESULT = jumpTriplet.getIndex();
              :};


//convDate
conv_date ::= CONVDATE OPEN_BRACKET DATE_LITERAL:date CLOSE_BRACKET
{:
    System.out.println("convDate detected: " + date);

    String raw = (String)date;
    String[] parts = raw.split("-");

    if (parts.length != 3) {
        throw new InvalidDateFormat("Formato de fecha invalido: " + raw);
    }

    // Parsear los componentes de la fecha
    int d = Integer.parseInt(parts[0]);
    int m = Integer.parseInt(parts[1]);
    int y = Integer.parseInt(parts[2]);

    // Validar que la fecha sea valida
    try {
        java.time.LocalDate.of(y, m, d);
    } catch (java.time.DateTimeException ex) {
        throw new InvalidDateFormat("Fecha no valida: " + raw);
    } 

    // Generar tercetos para los componentes de la fecha (día, mes, año)
    int dayIdx = IntermediateCodeGenerator.createTriplet(Operator.CTE, "_" + d).getIndex();
    TypeTable.putType(dayIdx, DataType.INTEGER_TYPE);

    int monthIdx = IntermediateCodeGenerator.createTriplet(Operator.CTE, "_" + m).getIndex();
    TypeTable.putType(monthIdx, DataType.INTEGER_TYPE);

    int yearIdx = IntermediateCodeGenerator.createTriplet(Operator.CTE, "_" + y).getIndex();
    TypeTable.putType(yearIdx, DataType.INTEGER_TYPE);

    // Generar el valor convertido (formato YYYYMMDD)
    String yyyymmdd = String.format("%04d%02d%02d", y, m, d);

    // Crear un terceto especial para la conversión de fecha
    // Este terceto representa la operación CONVDATE con los tres componentes
    int convDateIdx = IntermediateCodeGenerator.createTriplet(Operator.CTE, "_" + yyyymmdd).getIndex();
    TypeTable.putType(convDateIdx, DataType.DATECONVERTED_TYPE);

    System.out.println("Fecha convertida: " + yyyymmdd + " (dia=" + d + ", mes=" + m + ", anio=" + y + ")");

    RESULT = yyyymmdd;
:};